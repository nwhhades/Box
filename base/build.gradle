plugins {
    id 'com.android.library'
    id 'maven-publish'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

def libName = "base"
def groupPackageName = "com.github.nwhhades"

android {
    compileSdk 32

    defaultConfig {
        minSdk 19
        targetSdk 32
        versionCode 200
        versionName "2.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    android.libraryVariants.all { variant ->
        // aar 输出文件名配置
        variant.outputs.all { output ->
            outputFileName = "${libName}-${android.defaultConfig.versionName}.aar"
        }
    }

    publishing {
        singleVariant("release")
    }

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                //组包名
                groupId = groupPackageName
                //依赖库名称
                artifactId = libName
                //依赖库版本号
                version = android.defaultConfig.versionName
            }
        }
    }
}

dependencies {

    api 'androidx.appcompat:appcompat:1.5.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    //工具箱-不需要混淆规则
    api 'com.blankj:utilcodex:1.31.1'
    //图片库-不需要混淆规则
    api 'com.github.bumptech.glide:glide:4.14.2'
    //吐司框架-不需要混淆 https://github.com/getActivity/Toaster
    api 'com.github.getActivity:Toaster:12.0'
    //权限请求框架-不需要混淆 https://github.com/getActivity/XXPermissions
    api 'com.github.getActivity:XXPermissions:16.8'

}